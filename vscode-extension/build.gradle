plugins {
  id "com.moowork.node" version "1.3.1"
}

task build {
}

task copyJar(type: Copy) {
    from "$projectDir/../build/libs/groovy-language-server-all.jar"
    into "$buildDir/bin"
}

task copyLocal(type: Copy) {
    from "$projectDir/package.json"
    into "$buildDir"
}

task webpack(type: Exec) {
  inputs.file("package.json")
  inputs.file("package-lock.json")
  inputs.file("webpack.config.js")
  inputs.file("tsconfig.json")
  inputs.dir("src")
  outputs.file("$buildDir/extension.js")
  outputs.file("$buildDir/extension.js.map")
  
  def webpack = "$projectDir/node_modules/.bin/webpack"
  if (System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")) {
    webpack += ".cmd"
  }
  commandLine "$webpack", "--mode", "production"
}

task vsce(type: Exec) {
  inputs.dir("$buildDir")
  outputs.file("$buildDir/groovy-0.0.0.vsix")

  def vsce = "$projectDir/node_modules/.bin/vsce"
  if (System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")) {
    vsce += ".cmd"
  }
  workingDir "$buildDir"
  commandLine "$vsce", "package"
}

tasks.webpack.dependsOn tasks.npmInstall
tasks.vsce.dependsOn tasks.copyJar
tasks.vsce.dependsOn tasks.copyLocal
tasks.vsce.dependsOn tasks.webpack
tasks.build.dependsOn tasks.vsce

